plugins {
    id "org.asciidoctor.jvm.convert" version "3.1.0"
}

apply plugin: 'maven-publish'

def documentationDir = "doc/${project.version}/${project.jdk}"

asciidoctor {
    doFirst {
        frameworkModules.each {
            def moduleDir = it.projectDir
            def graphFilePath = "${moduleDir}/build/reports/dependency-graph/dependency-graph.svg"
            graphFilePath = graphFilePath.replace(rootDir.toString(), '').substring(1)
            ant.copy(todir: "${buildDir}/docs/asciidoc/images/modules") {
                fileset(dir: rootDir, includes: graphFilePath)
            }
        }
    }
    baseDirFollowsSourceFile()
    sources {
        include '*.adoc', 'technical/**/*.adoc', 'tutorials/**/*.adoc', 'manual/**/*.adoc'
    }
    attributes toc: 'right', sectnums: 4, 'jminor-version': project.version, prewrap: false, experimental: true
    asciidoctorj {
        version = '2.2.0'
        modules {
            diagram.use()
            diagram.version '1.5.16'
        }
        attributes 'source-highlighter': 'prettify'
    }
    //possible code changes
    outputs.upToDateWhen { false }
}

task combinedJavadoc(type: Javadoc) {
    group 'documentation'
    title "JMinor Framework API $project.version"
    failOnError = false
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    destinationDir = file("${buildDir}/docs/javadoc")
    source frameworkModules.collect { it.sourceSets.main.allJava }
    exclude '**/module-info.java'//temporary fix for 'too many module declarations found' error
    classpath = files(frameworkModules.collect { it.sourceSets.main.compileClasspath })
}

task assembleDocs(dependsOn: [combinedJavadoc, asciidoctor]) {
    group 'documentation'
    def docFolder = "${buildDir}/docs/${documentationDir}"
    doLast {
        ant.delete(dir: docFolder)
        ant.copy(todir: docFolder) {
            fileset(dir: "${buildDir}/docs/asciidoc")
            fileset(file: 'favicon.ico')
        }
        ant.copy(todir: docFolder + '/api') {
            fileset(dir: "${buildDir}/docs/javadoc")
        }
    }
}

task copyToGitHubPages(type: Sync, dependsOn: assembleDocs) {
    group 'documentation'
    def ghPagesDocDir = "../../gh-pages/${documentationDir}"
    from "${buildDir}/docs/${documentationDir}"
    into ghPagesDocDir
}

task documentationZip(type: Zip, dependsOn: assembleDocs) {
    group 'documentation'
    from "${buildDir}/docs/${documentationDir}"
}

artifacts {
    archives documentationZip
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.jminor.' + project.jdk
            artifact documentationZip
        }
    }
}