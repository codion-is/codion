buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.javamodularity:moduleplugin:1.6.0"
    }
}

plugins {
    id "org.sonarqube" version "2.8"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "com.vanniktech.dependency.graph.generator" version "0.5.0"
}

ext {
    frameworkModules = subprojects.findAll {
        project -> !project.name.contains("demos") && !project.name.contains("documentation")
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

configure(project.frameworkModules) {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'project-report'
    apply plugin: 'com.vanniktech.dependency.graph.generator'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }

    clean {
        doLast {
            //clean intellij output dir as well
            file('out').deleteDir()
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    ext.moduleName = "org." + project.name.replace('-', '.')

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
//        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    javadoc {
        options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        failOnError = false
    }

    jar {
        manifest {
            attributes(
                    "Sealed": "true",
                    "Specification-Title": project.name,
                    "Specification-Version": project.version,
                    "Specification-Vendor": "JMinor",
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Implementation-Vendor": "JMinor",
                    "Implementation-Vendor-Id": "org.jminor",
                    "Implementation-URL": "https://jminor.org",
                    "Automatic-Module-Name": "org." + project.name.replace('-', '.'),
                    "Build-Jdk": org.gradle.internal.jvm.Jvm.current(),
                    "Built-By": System.getProperty("user.name"),
                    "Build-Date": java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd").format(LocalDate.now()))
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'org.jminor.' + project.jdk
                from components.java
            }
        }
        repositories {
            maven {
                credentials {
                    username "${artifactoryUsername}"
                    password "${artifactoryPassword}"
                }
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "${artifactorySnapshotUrl}"
                } else {
                    url "${artifactoryReleaseUrl}"
                }
            }
        }
    }

    sonarqube {
        System.setProperty("sonar.host.url", "${sonarHostUrl}")
        System.setProperty("sonar.login", "${sonarLogin}")
        System.setProperty("sonar.password", "${sonarPassword}")
        properties {
            property "sonar.projectName", "org." + project.name.replace('-', '.')
            property "sonar.projectKey", "org.jminor:jminor"
            property "sonar.projectVersion", project.version
            property "sonar.java.source", "1.8"
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.exclusions", "**/*TestDomain.java"
        }
    }

    /** Creates a key- and truststore pair used when running server unit tests and demos with remote connection */
    task createServerKeystore {
        def keystoreDir = "${rootDir}/framework/server/src/main/security/"
        def keystore = keystoreDir + 'jminor_keystore.jks'
        def truststore = keystoreDir + 'jminor_truststore.jks'
        def certificate = keystoreDir + 'jminor_server.cer'
        def keyToolExecutable = System.getProperty('java.home') + '/bin/keytool'

        onlyIf {!file(keystore).exists()}

        doLast {
            ant.genkey(alias: 'JMinorServer', keyalg: 'RSA', keystore: keystore,
                    storepass: 'crappypass', dname: 'CN=Dummy, OU=dummy, O=dummy.org, C=DU', storetype: 'pkcs12')
            exec {
                executable = keyToolExecutable
                args = ['-exportcert', '-keystore', keystore, '-storepass', 'crappypass',
                        '-alias', 'JMinorServer', '-rfc', '-file', certificate]
            }
            exec {
                executable = keyToolExecutable
                args = ['-import', '-alias', 'JMinorServer', '-storepass', 'crappypass', '-file', certificate,
                        '-keystore', truststore, '-noprompt', '-storetype', 'pkcs12']
            }
            ant.delete(file: certificate)
        }
    }

    test {
        moduleOptions {
            addExports = [
                    'org.junit.platform.commons/org.junit.platform.commons.util': 'ALL-UNNAMED',
                    'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
            ]
        }
        useJUnitPlatform()
        systemProperty "jminor.db.type", "h2"
        systemProperty "jminor.db.url", "jdbc:h2:mem:h2db"
        systemProperty "jminor.db.initScript", "src/test/sql/create_h2_db.sql"
        systemProperty "jminor.test.user", "scott:tiger"
    }.dependsOn createServerKeystore
}

task tagRelease {
    doLast {
        if (project.version.contains("SNAPSHOT")) {
            throw new GradleException("Thou shalt not tag a snapshot release")
        }
        def tagName = 'v' + project.version + '-' + project.jdk
        exec { commandLine 'git', 'push' }
        exec { commandLine 'git', 'tag', '-a', tagName, '-m', '"' + tagName + ' release"' }
        exec { commandLine 'git', 'push', 'origin', tagName }
    }
}