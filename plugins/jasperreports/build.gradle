dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')

    testImplementation project(':codion-common-http')
    testImplementation project(':codion-framework-db-local')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    implementation "commons-logging:commons-logging:${commonsLoggingVersion}"
    implementation ("commons-digester:commons-digester:${commonsDigesterVersion}") {
        transitive = false
    }
    implementation "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    implementation ("commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}") {
        transitive = false
    }
    implementation "org.eclipse.jdt:ecj:${jdtCoreCompilerVersion}"

    testImplementation project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['is.codion.common.http,is.codion.dbms.h2database,is.codion.framework.db.local']
    testAddReads = ['is.codion.plugin.jasperreports': 'is.codion.common.http,is.codion.dbms.h2database,is.codion.framework.domain,is.codion.framework.db.core,is.codion.framework.db.local']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/test/reports')
    def jasperTargetDir = file('build/resources/test')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
compileJava.finalizedBy buildReports

test {
    dependsOn buildReports
}