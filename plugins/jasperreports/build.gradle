dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')

    testImplementation project(':jminor-common-http')
    testImplementation project(':jminor-framework-db-local')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    implementation "commons-logging:commons-logging:${commonsLoggingVersion}"
    implementation "commons-digester:commons-digester:${commonsDigesterVersion}"
    implementation "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    implementation "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    implementation "org.eclipse.jdt.core.compiler:ecj:${jdtCoreCompilerVersion}"

    testImplementation project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['org.jminor.common.remote.http,org.jminor.dbms.h2database,org.jminor.framework.db.local']
    testAddReads = ['org.jminor.plugin.jasperreports': 'org.jminor.common.remote.http,org.jminor.dbms.h2database,org.jminor.framework.domain,org.jminor.framework.db.core,org.jminor.framework.db.local']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/test/reports')
    def jasperTargetDir = file('build/resources/test')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
compileJava.finalizedBy buildReports