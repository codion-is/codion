dependencies {
    api project(':jminor-common-core')
    api project(':jminor-common-db')
    api project(':jminor-common-model')

    api project(':jminor-framework-domain')
    api project(':jminor-framework-db-core')

    testImplementation project(':jminor-framework-db-local')

    runtimeOnly "org.json:json:${jsonVersion}"

    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

//base unit test classes that implementation modules (swing/fx) can depend on
task jarTests(type: Jar, dependsOn: testClasses) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
    include('org/jminor/framework/model/Abstract**', 'org/jminor/framework/model/TestDomain**')
    duplicatesStrategy = 'exclude'
    manifest {
        attributes(
                "Specification-Title": project.name,
                "Specification-Version": "${version}",
                "Specification-Vendor": "JMinor",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor": "JMinor",
                "Implementation-Vendor-Id": "org.jminor",
                "Implementation-URL": "http://jminor.org",
                "Automatic-Module-Name": moduleName,
                "Build-Jdk": org.gradle.internal.jvm.Jvm.current(),
                "Built-By": System.getProperty("user.name")/*,
                "Build-Time": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")*/)
    }
}
configurations {
    testArtifact.extendsFrom testRuntime
}
artifacts {
    testArtifact jarTests
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jarTests
        }
    }
}