dependencies {
    api project(':jminor-common-core')
    api project(':jminor-common-db')
    api project(':jminor-common-model')

    api project(':jminor-framework-domain')
    api project(':jminor-framework-db-core')

    testImplementation project(':jminor-framework-db-local')

    runtimeOnly "org.json:json:${jsonVersion}"

    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['java.sql,org.jminor.framework.db.local']
    testAddReads = ['org.jminor.framework.model': 'java.sql,org.jminor.framework.db.local']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

//base unit test classes that implementation modules (swing/fx) can depend on
task testJar(type: Jar, dependsOn: testClasses) {
    group 'build'
    archiveClassifier = 'tests'
    from sourceSets.test.output
    include('org/jminor/framework/model/Abstract**', 'org/jminor/framework/model/TestDomain**')
    manifest {
        attributes(
                "Specification-Title": project.name,
                "Specification-Version": project.version,
                "Specification-Vendor": "JMinor",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "JMinor",
                "Implementation-Vendor-Id": "org.jminor",
                "Implementation-URL": "http://jminor.org",
                "Build-Jdk": org.gradle.internal.jvm.Jvm.current(),
                "Built-By": System.getProperty("user.name")/*,
                "Build-Time": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")*/)
    }
}
configurations {
    testArtifact.extendsFrom testRuntime
}
artifacts {
    testArtifact testJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact testJar
        }
    }
}