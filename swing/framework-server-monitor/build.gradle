import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply plugin: 'application'

dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')
    implementation project(':jminor-common-model')
    implementation project(':jminor-common-remote')

    implementation project(':jminor-framework-db-remote')
    implementation project(':jminor-framework-server')
    implementation project(':jminor-swing-common-model')
    implementation project(':jminor-swing-common-ui')

    testImplementation project(':jminor-framework-domain')
    testImplementation project(':jminor-framework-db-core')
    testImplementation project(':jminor-framework-db-remote')
    testRuntimeOnly project(':jminor-framework-db-local')

    implementation "org.json:json:${jsonVersion}"
    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"

    runtimeOnly project(':jminor-plugin-logback-proxy')
}

ext {
    testAddModules = ['org.jminor.framework.db.core,org.jminor.framework.db.remote']
    testAddReads = ['org.jminor.swing.framework.server.monitor': 'org.jminor.framework.db.core,org.jminor.framework.db.remote']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

mainClassName = "$moduleName/org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel"

task runServerMonitor(type: ModularJavaExec) {
    group 'run'
    classpath = sourceSets.main.runtimeClasspath
    main = mainClassName
    jvmArgs = ['--add-modules', 'org.jminor.plugin.logback.proxy']
    systemProperties = [
            'jminor.server.hostname'          : 'localhost',
            'jminor.server.admin.user'        : 'scott:tiger',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/config/logback.xml'
    ]
}

applicationDistribution.from(files(
        '../../framework/server/src/main/security/jminor_truststore.jks',
        '../../framework/server/src/main/security/all_permissions.policy',
        'src/main/config/logback.xml',
        'src/main/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        '--add-modules', 'org.jminor.plugin.logback.proxy',
        '-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks',
        '-Djavax.net.ssl.trustStorePassword=crappypass',
        '-Djava.security.policy=../config/all_permissions.policy',
        '-Dlogback.configurationFile=../config/logback.xml',
        '-Djminor.configurationFile=../config/server_monitor.config'
]