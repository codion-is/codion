apply plugin: 'application'

dependencies {
    implementation project(':org.jminor.common.core')
    implementation project(':org.jminor.common.db')
    implementation project(':org.jminor.common.model')
    implementation project(':org.jminor.common.server')

    implementation project(':org.jminor.framework.server')
    implementation project(':org.jminor.swing.common.model')
    implementation project(':org.jminor.swing.common.ui')

    testImplementation project(':org.jminor.framework.db.core')
    testImplementation project(':org.jminor.framework.db.remote')
    testRuntime project(':org.jminor.framework.db.local')
    testRuntime project(':org.jminor.framework.model.unittests')

    implementation "org.json:json:${jsonVersion}"
    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreechartVersion}"

    testRuntime "com.h2database:h2:${h2Version}"

    runtime project(':org.jminor.plugin.logback.proxy')
}

mainClassName = 'org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel'

applicationDistribution.from(files(
        '../../resources/security/JMinorClientTruststore',
        '../../resources/security/jminor_server_monitor.policy',
        'resources/config/logback.xml',
        'resources/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        "-Djavax.net.ssl.trustStore=../config/JMinorClientTruststore",
        "-Djava.security.policy=../config/jminor_server_monitor.policy",
        "-Djminor.configurationFile=../config/server_monitor.config"
]

task runServerMonitor(type: JavaExec) {
    group 'run'
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel'
    systemProperties = [
            'jminor.server.hostname': 'localhost',
            'jminor.server.admin.user': 'scott:tiger',
            'javax.net.ssl.trustStore': '../../resources/security/JMinorClientTruststore',
            'java.security.policy': '../../resources/security/jminor_server_monitor.policy',
            'logback.configurationFile': 'resources/config/logback.xml'
    ]
}