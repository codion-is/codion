apply plugin: 'application'

dependencies {
    compile project(':org.jminor.common.core')
    compile project(':org.jminor.common.db')
    compile project(':org.jminor.common.model')
    compile project(':org.jminor.common.server')

    compile project(':org.jminor.framework.server')
    compile project(':org.jminor.swing.common.model')
    compile project(':org.jminor.swing.common.ui')

    testRuntime project(':org.jminor.framework.db.core')
    testRuntime project(':org.jminor.framework.db.local')
    testRuntime project(':org.jminor.framework.db.remote')
    testRuntime project(':org.jminor.framework.model.unittests')

    compile "org.json:json:${jsonVersion}"
    compile "org.jfree:jcommon:${jcommonVersion}"
    compile "org.jfree:jfreechart:${jfreechartVersion}"

    testRuntime "com.h2database:h2:${h2Version}"

    runtime project(':org.jminor.plugin.logback.proxy')
}

mainClassName = "org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel"

applicationDistribution.from(files(
        '../../resources/security/JMinorServerMonitorTruststore',
        '../../resources/security/jminor_server_monitor.policy',
        'resources/config/logback.xml',
        'resources/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        "-Djavax.net.ssl.trustStore=../config/JMinorServerMonitorTruststore",
        "-Djava.security.policy=../config/jminor_server_monitor.policy",
        "-Djminor.configurationFile=../config/server_monitor.config"
]

task runServerMonitor() {
    doLast {
        ant.java(
                classname: 'org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel',
                classpath: sourceSets.main.runtimeClasspath.asPath,
                fork: true
        ) {
            sysproperty(key: 'jminor.server.hostname', value: 'localhost')
            sysproperty(key: 'jminor.server.admin.user', value: 'scott:tiger')
            sysproperty(key: 'javax.net.ssl.trustStore', value: '../../resources/security/JMinorServerMonitorTruststore')
            sysproperty(key: 'java.security.policy', value: '../../resources/security/jminor_server_monitor.policy')
            sysproperty(key: 'logback.configurationFile', value: 'resources/config/logback.xml')
        }
    }
}