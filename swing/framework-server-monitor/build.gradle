apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')
    implementation project(':codion-common-rmi')

    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')

    implementation project(':codion-framework-server')

    implementation "com.formdev:flatlaf-intellij-themes:${flatLafVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtimeOnly project(':codion-plugin-logback-proxy')

    testImplementation project(':codion-framework-server')
    testImplementation project(':codion-framework-db-rmi')
    testCompileOnly project(':codion-framework-db-local')//for modules
    testRuntimeOnly project(':codion-plugin-hikari-pool')
    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

mainClassName = 'is.codion.swing.framework.server.monitor.ui.EntityServerMonitorPanel'

applicationDistribution.from(files(
        '../../framework/server/src/main/config/truststore.jks',
        '../../framework/server/src/main/config/all_permissions.policy',
        'src/main/config/logback.xml',
        'src/main/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        "-Dcodion.client.trustStore=../config/truststore.jks",
        "-Dcodion.client.trustStorePassword=crappypass",
        "-Djava.security.policy=../config/all_permissions.policy",
        "-Dcodion.configurationFile=../config/server_monitor.config"
]

task runServerMonitor(type: JavaExec) {
    group 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = mainClassName
    systemProperties = [
            'codion.server.hostname'          : "${serverHostName}",
            'codion.server.admin.user'        : 'scott:tiger',
            'codion.client.trustStore'        : '../../framework/server/src/main/config/truststore.jks',
            'codion.client.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/config/logback.xml'
    ]
}