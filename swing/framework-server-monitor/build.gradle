apply plugin: 'application'

dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')
    implementation project(':jminor-common-model')
    implementation project(':jminor-common-remote')

    implementation project(':jminor-swing-common-model')
    implementation project(':jminor-swing-common-ui')

    implementation project(':jminor-framework-server')

    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtimeOnly project(':jminor-plugin-logback-proxy')

    testImplementation project(':jminor-framework-server')
    testImplementation project(':jminor-framework-db-remote')
    testCompileOnly project(':jminor-framework-db-local')//for modules
    testRuntimeOnly project(':jminor-plugin-hikari-pool')
    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

mainClassName = 'org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel'

applicationDistribution.from(files(
        '../../framework/server/src/main/security/jminor_truststore.jks',
        '../../framework/server/src/main/security/all_permissions.policy',
        'src/main/config/logback.xml',
        'src/main/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        "-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks",
        "-Djavax.net.ssl.trustStorePassword=crappypass",
        "-Djava.security.policy=../config/all_permissions.policy",
        "-Djminor.configurationFile=../config/server_monitor.config"
]

task runServerMonitor(type: JavaExec) {
    group 'run'
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jminor.swing.framework.server.monitor.ui.EntityServerMonitorPanel'
    systemProperties = [
            'jminor.server.hostname'          : 'localhost',
            'jminor.server.admin.user'        : 'scott:tiger',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/config/logback.xml'
    ]
}