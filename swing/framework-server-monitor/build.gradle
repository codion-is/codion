apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')
    implementation project(':codion-common-rmi')

    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')

    implementation project(':codion-framework-server')

    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtimeOnly project(':codion-plugin-logback-proxy')

    testImplementation project(':codion-framework-server')
    testImplementation project(':codion-framework-db-rmi')
    testCompileOnly project(':codion-framework-db-local')//for modules
    testRuntimeOnly project(':codion-plugin-hikari-pool')
    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

mainClassName = 'dev.codion.swing.framework.server.monitor.ui.EntityServerMonitorPanel'

applicationDistribution.from(files(
        '../../framework/server/src/main/security/codion_truststore.jks',
        '../../framework/server/src/main/security/all_permissions.policy',
        'src/main/config/logback.xml',
        'src/main/config/server_monitor.config')) {
    into("config")
}

applicationDefaultJvmArgs = [
        "-Djavax.net.ssl.trustStore=../config/codion_truststore.jks",
        "-Djavax.net.ssl.trustStorePassword=crappypass",
        "-Djava.security.policy=../config/all_permissions.policy",
        "-Dcodion.configurationFile=../config/server_monitor.config"
]

task runServerMonitor(type: JavaExec) {
    group 'run'
    classpath = sourceSets.main.runtimeClasspath
    main = 'dev.codion.swing.framework.server.monitor.ui.EntityServerMonitorPanel'
    systemProperties = [
            'codion.server.hostname'          : 'localhost',
            'codion.server.admin.user'        : 'scott:tiger',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/codion_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/config/logback.xml'
    ]
}