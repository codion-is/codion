apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')
    implementation project(':codion-common-rmi')

    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')

    implementation project(':codion-framework-server')

    implementation(libs.flatlaf.intellij.themes)
    implementation(libs.slf4j.api)

    implementation(libs.jcommon)
    implementation(libs.jfreechart)

    runtimeOnly project(':codion-plugin-logback-proxy')

    testImplementation project(':codion-framework-server')
    testImplementation project(':codion-framework-db-rmi')
    testCompileOnly project(':codion-framework-db-local')//for modules
    testRuntimeOnly project(':codion-plugin-hikari-pool')
    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly(libs.h2)
}

application {
    mainClass = 'is.codion.swing.framework.server.monitor.ui.EntityServerMonitorPanel'
}

applicationDistribution.from(files(
        '../../framework/server/src/main/config/truststore.jks',
        'src/main/config/logback.xml')) {
    into("config")
}

applicationDefaultJvmArgs = [
        '-Dcodion.server.hostname=' + getProperty("serverHostName"),
        '-Dcodion.server.admin.user=scott:tiger',
        '-Dcodion.client.trustStore=../config/truststore.jks',
        '-Dcodion.client.trustStorePassword=crappypass',
        '-Dlogback.configurationFile=../config/logback.xml'
]

tasks.register('runServerMonitor', JavaExec) {
    group 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set(application.mainClass)
    systemProperties = [
            'codion.server.hostname'          : "${serverHostName}",
            'codion.server.admin.user'        : 'scott:tiger',
            'codion.client.trustStore'        : '../../framework/server/src/main/config/truststore.jks',
            'codion.client.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/config/logback.xml'
    ]
}