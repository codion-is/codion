dependencies {
    api project(':jminor-common-core')
    api project(':jminor-common-db')
    api project(':jminor-common-model')

    api project(':jminor-framework-domain')
    implementation project(':jminor-framework-db-core')
    api project(':jminor-framework-model')

    api project(':jminor-swing-common-model')

    testImplementation project(':jminor-framework-db-local')
    testImplementation project(path: ':jminor-framework-model', configuration: 'testArtifact')
    testImplementation project(':jminor-swing-common-ui')

    implementation "org.json:json:${jsonVersion}"

    runtimeOnly "com.toedter:jcalendar:${jcalendarVersion}"

    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['org.jminor.framework.db.local,org.jminor.framework.model,org.jminor.swing.common.ui']
    testAddReads = [
            'org.jminor.swing.framework.model': 'org.jminor.framework.db.local,org.jminor.framework.model,org.jminor.swing.common.ui',
            'org.jminor.framework.model': 'org.jminor.framework.db.local,org.junit.jupiter.api'
    ]
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
    doFirst {
        options.compilerArgs.add('--patch-module')
        options.compilerArgs.add("org.jminor.framework.model=${projectDir}/../../framework/model/build/libs/jminor-framework-model-${version}-tests.jar")
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
    doFirst {
        jvmArgs = [
                '--patch-module', "org.jminor.framework.model=${projectDir}/../../framework/model/build/libs/jminor-framework-model-${version}-tests.jar",
        ]
    }
}