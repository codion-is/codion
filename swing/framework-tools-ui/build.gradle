import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')
    api project(':codion-swing-framework-tools')

    testRuntimeOnly project(':codion-framework-db-local')
    testRuntimeOnly project(':codion-framework-db-rmi')
    testRuntimeOnly project(':codion-framework-server')

    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

test {
    systemProperty "codion.db.url", "jdbc:h2:mem:h2db"
    systemProperty "codion.db.initScript", "src/main/config/create_schema.sql"
    systemProperty "codion.test.user", "scott:tiger"
}

ext {
    testAddModules = ['is.codion.common.db,is.codion.common.model,is.codion.swing.common.model']
    testAddReads = ['is.codion.swing.framework.tools.ui': 'is.codion.common.db,is.codion.common.model,is.codion.swing.common.model']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

distributions {
    main {
        distributionBaseName = "codion.entity.generator"
    }
}

applicationName = 'entity_generator'

mainClassName = '$moduleName/is.codion.swing.framework.tools.ui.generator.EntityGeneratorPanel'

applicationDefaultJvmArgs = [
        '-Dcodion.configurationFile=../config/entity_generator.config',
        '-Dlogback.configurationFile=../config/logback.xml'
]

applicationDistribution.into('config') {
    from(files('src/main/config/entity_generator.config'))
    from(files('src/main/config/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
}

task runEntityGenerator(type: ModularJavaExec) {
    group 'run'
    main = mainClassName
    classpath = sourceSets.test.runtimeClasspath
    systemProperties = [
            'codion.db.url'             : 'jdbc:h2:mem:h2db',
            'codion.db.initScript'      :
                    '../../demos/chinook/src/main/sql/create_schema.sql,' +
                    '../../demos/empdept/src/main/sql/create_schema.sql,' +
                    '../../demos/petclinic/src/main/sql/create_schema.sql,' +
                    '../../demos/petstore/src/main/sql/create_schema.sql,' +
                    '../../demos/world/src/main/sql/create_schema.sql'
    ]
}