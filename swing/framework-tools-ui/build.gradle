import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply plugin: 'application'

dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')
    implementation project(':jminor-common-model')

    implementation project(':jminor-swing-common-tools')
    implementation project(':jminor-swing-common-model')
    implementation project(':jminor-swing-common-ui')
    api project(':jminor-swing-framework-tools')

    testRuntimeOnly project(':jminor-framework-db-local')
    testRuntimeOnly project(':jminor-framework-db-remote')
    testRuntimeOnly project(':jminor-framework-server')

    testRuntimeOnly project(':jminor-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['org.jminor.common.db,org.jminor.common.model,org.jminor.swing.common.model']
    testAddReads = ['org.jminor.swing.framework.tools.ui': 'org.jminor.common.db,org.jminor.common.model,org.jminor.swing.common.model']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

distributions {
    main {
        baseName = "jminor.entity.generator"
    }
}

applicationName = 'entity_generator'

mainClassName = "$moduleName/org.jminor.swing.framework.tools.ui.EntityGeneratorPanel"

task runEntityGenerator(type: ModularJavaExec) {
    group 'run'
    main = mainClassName
    classpath = sourceSets.test.runtimeClasspath
    systemProperties = [
            'jminor.db.type'            : 'h2',
            'jminor.db.host'            : 'h2db',
            'jminor.db.embeddedInMemory': 'true',
            'jminor.db.initScript'      : 'src/main/config/create_schema.sql',
            'logback.configurationFile' : 'src/main/config/logback.xml'
    ]
}

applicationDefaultJvmArgs = [
        '-Djminor.configurationFile=../config/entity_generator.config',
        '-Dlogback.configurationFile=../config/logback.xml'
]

applicationDistribution.into('config') {
    from(files('src/main/config/entity_generator.config'))
    from(files('src/main/config/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
}