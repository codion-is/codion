import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')
    api project(':codion-swing-framework-tools')

    testRuntimeOnly project(':codion-framework-db-local')
    testRuntimeOnly project(':codion-framework-db-rmi')
    testRuntimeOnly project(':codion-framework-server')

    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['dev.codion.common.db,dev.codion.common.model,dev.codion.swing.common.model']
    testAddReads = ['dev.codion.swing.framework.tools.ui': 'dev.codion.common.db,dev.codion.common.model,dev.codion.swing.common.model']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

distributions {
    main {
        distributionBaseName = "codion.entity.generator"
    }
}

applicationName = 'entity_generator'

mainClassName = '$moduleName/dev.codion.swing.framework.tools.ui.generator.EntityGeneratorPanel'

applicationDefaultJvmArgs = [
        '-Dcodion.configurationFile=../config/entity_generator.config',
        '-Dlogback.configurationFile=../config/logback.xml'
]

applicationDistribution.into('config') {
    from(files('src/main/config/entity_generator.config'))
    from(files('src/main/config/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
}

task runEntityGenerator(type: ModularJavaExec) {
    group 'run'
    main = mainClassName
    classpath = sourceSets.test.runtimeClasspath
    systemProperties = [
            'codion.db.url'             : 'jdbc:h2:mem:h2db',
            'codion.db.embeddedInMemory': 'true',
            'codion.db.initScript'      : 'src/main/config/create_schema.sql',
            'logback.configurationFile' : 'src/main/config/logback.xml'
    ]
}