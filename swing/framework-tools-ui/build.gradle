import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply plugin: 'application'

dependencies {
    implementation project(':codion-common-core')
    implementation project(':codion-common-db')
    implementation project(':codion-common-model')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-model')
    implementation project(':codion-swing-common-ui')
    api project(':codion-swing-framework-tools')

    testRuntimeOnly project(':codion-framework-db-local')
    testRuntimeOnly project(':codion-framework-db-rmi')
    testRuntimeOnly project(':codion-framework-server')

    testRuntimeOnly project(':codion-dbms-h2database')
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    testAddModules = ['is.codion.common.db,is.codion.common.model,is.codion.swing.common.model']
    testAddReads = ['is.codion.swing.framework.tools.ui': 'is.codion.common.db,is.codion.common.model,is.codion.swing.common.model']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

distributions {
    main {
        distributionBaseName = "codion.database.explorer"
    }
}

applicationName = 'database_explorer'

mainClassName = '$moduleName/is.codion.swing.framework.tools.ui.explorer.DatabaseExplorerPanel'

applicationDefaultJvmArgs = [
        '-Dcodion.configurationFile=../config/database_explorer.config',
        '-Dlogback.configurationFile=../config/logback.xml'
]

applicationDistribution.into('config') {
    from(files('src/main/config/database_explorer.config'))
    from(files('src/main/config/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
}

task runDatabaseExplorer(type: ModularJavaExec) {
    group 'run'
    main = 'is.codion.swing.framework.tools.ui.explorer.DatabaseExplorerPanel'
    classpath = sourceSets.test.runtimeClasspath
    systemProperties = [
            'codion.db.url'        : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts':
                    '../../demos/chinook/src/main/sql/create_schema.sql,' +
                    '../../demos/empdept/src/main/sql/create_schema.sql,' +
                    '../../demos/petclinic/src/main/sql/create_schema.sql,' +
                    '../../demos/petstore/src/main/sql/create_schema.sql,' +
                    '../../demos/world/src/main/sql/create_schema.sql'
    ]
}