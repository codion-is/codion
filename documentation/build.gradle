plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

apply plugin: 'maven-publish'

def documentationVersion = project.version.replace('-SNAPSHOT', '')

def documentationDir = "doc/${documentationVersion}/${project.targetJdk}"

asciidoctor {
    // since the sources included in the docs may have changed, there's definitely
    // a more gradle like way to do this, but it escapes me
    inputs.dir(new File("../demos/chinook/src"));
    inputs.dir(new File("../demos/empdept/src"));
    inputs.dir(new File("../demos/manual/src"));
    inputs.dir(new File("../demos/petclinic/src"));
    inputs.dir(new File("../demos/petstore/src"));
    inputs.dir(new File("../demos/world/src"));
    // config files
    inputs.dir(new File("../framework/server/src/main"));

    doFirst {
        frameworkModules.each {
            def moduleDir = it.projectDir
            def graphFilePath = "${moduleDir}/build/reports/dependency-graph/dependency-graph.svg"
            graphFilePath = graphFilePath.replace(rootDir.toString(), '').substring(1)
            ant.copy(todir: "${buildDir}/docs/asciidoc/images/modules") {
                fileset(dir: rootDir, includes: graphFilePath)
            }
        }
    }
    baseDirFollowsSourceFile()
    sources {
        include '*.adoc', 'technical/**/*.adoc', 'tutorials/**/*.adoc', 'manual/**/*.adoc', 'help/**/*.adoc'
    }
    attributes 'codion-version': documentationVersion, 'codion-jdk': project.targetJdk,
            revnumber: documentationVersion, sectnums: 4, sectanchors: true, prewrap: false,
            experimental: true, reproducible: true, linkcss: true, googleAnalyticsId: 'UA-159091863-1',
            'common-core': '/is.codion.common.core',
            'common-db': '/is.codion.common.db',
            'common-http': '/is.codion.common.http',
            'common-model': '/is.codion.common.model',
            'common-rmi': '/is.codion.common.rmi',
            'framework-db-core': '/is.codion.framework.db.core',
            'framework-db-http': '/is.codion.framework.db.http',
            'framework-db-local': '/is.codion.framework.db.local',
            'framework-db-rmi': '/is.codion.framework.db.rmi',
            'framework-domain': '/is.codion.framework.domain',
            'framework-domain-test': '/is.codion.framework.domain.test',
            'framework-model': '/is.codion.framework.model',
            'framework-model-test': '/is.codion.framework.model.test',
            'framework-server': '/is.codion.framework.server',
            'framework-servlet': '/is.codion.framework.servlet',
            'plugin-jackson-json': '/is.codion.plugin.jackson.json',
            'plugin-jasperreports': '/is.codion.plugin.jasperreports',
            'swing-common-model': '/is.codion.swing.common.model',
            'swing-common-ui': '/is.codion.swing.common.ui',
            'swing-framework-model': '/is.codion.swing.framework.model',
            'swing-framework-tools': '/is.codion.swing.framework.tools',
            'swing-framework-ui': '/is.codion.swing.framework.ui'
    asciidoctorj {
        version = '2.5.3'
        modules {
            diagram.use()
        }
        attributes 'source-highlighter': 'prettify'
    }
}

/**
 * An absolute monstrosity of a workaround for combining javadocs for multiple modular sub-projects.
 */
task combinedJavadoc {
    group 'documentation'
    FileCollection classpath = getProject().files()
    frameworkModules.each { module ->
        module.tasks.withType(Javadoc).each { javadocTask ->
            classpath += javadocTask.classpath
        }
    }
    doLast {
        def tempDir = "${buildDir}/tmp"
        def tempSrcDir = "${tempDir}/combinedSource";
        ant.delete(dir: tempSrcDir)
        frameworkModules.each { module ->
            ant.copy(todir: "${tempSrcDir}/" + module.properties.get("moduleName")) {
                fileset(dir: "${module.projectDir}/src/main/java")
            }
        }
        StandardJavadocDocletOptions options = new StandardJavadocDocletOptions();
        options.destinationDirectory(file("${buildDir}/docs/javadoc"))
        options.docTitle("Codion Framework API ${documentationVersion}")
        options.links("https://docs.oracle.com/en/java/javase/11/docs/api/")
        options.modulePath(new ArrayList(classpath.getFiles()))
        options.addStringOption("-module-source-path", tempSrcDir)

        List<String> sourceFileNames = new ArrayList();
        def iterator = getProject().getObjects().fileCollection().from(tempSrcDir).asFileTree.iterator()
        while (iterator.hasNext()) {
            sourceFileNames.add(((File) iterator.next()).getAbsolutePath());
        }
        options.setSourceNames(sourceFileNames);

        def optionsFile = new File(tempDir, "javadoc.options")
        options.write(optionsFile)

        exec {
            executable = System.getProperty('java.home') + '/bin/javadoc'
            args = ["@" + optionsFile.getAbsolutePath()]
        }
    }
}

task assembleDocs(dependsOn: [combinedJavadoc, asciidoctor]) {
    group 'documentation'
    def docFolder = "${buildDir}/docs/${documentationDir}"
    doLast {
        ant.delete(dir: docFolder)
        ant.copy(todir: docFolder) {
            fileset(dir: "${buildDir}/docs/asciidoc")
        }
        ant.copy(todir: docFolder + '/api') {
            fileset(dir: "${buildDir}/docs/javadoc")
        }
    }
}

task copyToGitHubPages(type: Sync, dependsOn: assembleDocs) {
    group 'documentation'
    def ghPagesDocDir = "../../codion-pages/${documentationDir}"
    from "${buildDir}/docs/${documentationDir}"
    into ghPagesDocDir
}

task documentationZip(type: Zip, dependsOn: assembleDocs) {
    group 'documentation'
    from "${buildDir}/docs/${documentationDir}"
}

artifacts {
    archives documentationZip
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'is.codion.' + project.targetJdk
            artifact documentationZip
        }
    }
}