plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

apply plugin: 'maven-publish'

def documentationVersion = project.version.replace('-SNAPSHOT', '')

def documentationDir = "${documentationVersion}/${project.targetJdk}"

task copyModuleDependencyGraphs {
    doLast {
        frameworkModules.each {
            def moduleDir = it.projectDir
            def graphFilePath = "${moduleDir}/build/reports/dependency-graph/dependency-graph.svg"
            graphFilePath = graphFilePath.replace(rootDir.toString(), '').substring(1)
            ant.copy(todir: "${buildDir}/asciidoc/images/modules") {
                fileset(dir: rootDir, includes: graphFilePath)
            }
        }
    }
}

asciidoctor {
    dependsOn copyModuleDependencyGraphs
    // since the sources included in the docs may have changed, there's definitely
    // a more gradle like way to do this, but it escapes me
    inputs.dir(new File("../demos/chinook/src"));
    inputs.dir(new File("../demos/empdept/src"));
    inputs.dir(new File("../demos/manual/src"));
    inputs.dir(new File("../demos/petclinic/src"));
    inputs.dir(new File("../demos/petstore/src"));
    inputs.dir(new File("../demos/world/src"));
    // config files
    inputs.dir(new File("../framework/server/src/main"));

    outputDir = "${buildDir}/asciidoc"

    baseDirFollowsSourceFile()
    sources {
        include '*.adoc', 'technical/**/*.adoc', 'tutorials/**/*.adoc', 'manual/**/*.adoc', 'help/**/*.adoc'
    }
    attributes 'codion-version': documentationVersion, 'codion-jdk': project.targetJdk,
            revnumber: documentationVersion, sectnums: 4, sectanchors: true, prewrap: false,
            experimental: true, reproducible: true, linkcss: true, googleAnalyticsId: 'UA-159091863-1',
            'opar': '-',
            'cpar': '-',
            'comma': '-',
            'common-core': '',
            'common-db': '',
            'common-http': '',
            'common-model': '',
            'common-rmi': '',
            'framework-db-core': '',
            'framework-db-http': '',
            'framework-db-local': '',
            'framework-db-rmi': '',
            'framework-domain': '',
            'framework-domain-test': '',
            'framework-model': '',
            'framework-model-test': '',
            'framework-server': '',
            'framework-servlet': '',
            'plugin-jackson-json': '',
            'plugin-jasperreports': '',
            'swing-common-model': '',
            'swing-common-ui': '',
            'swing-framework-model': '',
            'swing-framework-tools': '',
            'swing-framework-ui': ''
    asciidoctorj {
        version = '2.5.3'
        modules {
            diagram.use()
        }
        attributes 'source-highlighter': 'prettify'
    }
}

task combinedJavadoc(type: Javadoc) {
    group 'documentation'
    title "Codion Framework API ${documentationVersion}"
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
    destinationDir = file("${buildDir}/javadoc")
    frameworkModules.each { module ->
        module.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
        }
    }
}

task assembleDocs(dependsOn: [combinedJavadoc, asciidoctor]) {
    group 'documentation'
    def docFolder = "${buildDir}/${documentationDir}"
    doLast {
        ant.delete(dir: docFolder)
        ant.copy(todir: docFolder) {
            fileset(dir: "${buildDir}/asciidoc")
        }
        ant.copy(todir: docFolder + '/api') {
            fileset(dir: "${buildDir}/javadoc")
        }
    }
}

task copyToGitHubPages(type: Sync, dependsOn: assembleDocs) {
    group 'documentation'
    def ghPagesDocDir = "../../codion-pages/doc/${documentationDir}"
    from "${buildDir}/${documentationDir}"
    into ghPagesDocDir
}

task documentationZip(type: Zip, dependsOn: assembleDocs) {
    group 'documentation'
    from "${buildDir}/${documentationDir}"
}

artifacts {
    archives documentationZip
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'is.codion.' + project.targetJdk
            artifact documentationZip
        }
    }
}