apply from: '../demos.gradle'

dependencies {
    implementation project(':codion-framework-db-local')
    implementation project(':codion-framework-db-http')
    implementation project(':codion-framework-domain-test')
    implementation project(':codion-framework-server')
    implementation project(':codion-framework-servlet')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-tools-ui')
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-swing-framework-tools')

    implementation project(':codion-javafx-framework')

    implementation project(':codion-plugin-jasperreports')
    implementation project(':codion-plugin-jackson-json')

    testImplementation project(':codion-swing-framework-ui-test')

    implementation 'javax.ws.rs:jsr311-api:1.1.1'

    runtimeOnly project(':codion-plugin-logback-proxy')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext.execMainClass = 'is.codion.framework.demos.empdept.ui.EmpDeptAppPanel'
ext.execLoadTestMainClass = 'is.codion.framework.demos.empdept.testing.EmpDeptLoadTest'

test {
    systemProperty "codion.db.initScripts", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.empdept.domain.EmpDept"
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/empdept/domain')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.finalizedBy buildReports

startScripts {
    mainClass = execMainClass
}

clientRMIScript {
    mainClass = execMainClass
}

clientHttpScript {
    mainClass = execMainClass
}

loadTestRMIScript {
    mainClass = execLoadTestMainClass
}

loadTestHttpScript {
    mainClass = execLoadTestMainClass
}

runClientLocal {
    mainClass = execMainClass
}

task runClientLocalFX(type: JavaExec) {
    group 'application'
    mainClass = 'is.codion.framework.demos.empdept.javafx.EmpDeptFX'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts'       : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

runClientRMI {
    mainClass = execMainClass
}

runClientHttp {
    mainClass = execMainClass
}

runLoadTestRMI {
    mainClass = execLoadTestMainClass
}

runLoadTestHttp {
    mainClass = execLoadTestMainClass
}