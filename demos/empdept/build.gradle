import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply from: '../demos.gradle'

dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')
    implementation project(':jminor-common-model')
    implementation project(':jminor-common-server')

    implementation project(':jminor-framework-db-core')
    implementation project(':jminor-framework-db-local')
    implementation project(':jminor-framework-db-http')
    implementation project(':jminor-framework-db-test')
    implementation project(':jminor-framework-model')
    implementation project(':jminor-framework-server')
    implementation project(':jminor-framework-servlet')

    implementation project(':jminor-swing-common-model')
    implementation project(':jminor-swing-common-ui')
    implementation project(':jminor-swing-common-ui')
    implementation project(':jminor-swing-common-tools')
    implementation project(':jminor-swing-common-tools-ui')
    implementation project(':jminor-swing-framework-model')
    implementation project(':jminor-swing-framework-ui')
    implementation project(':jminor-swing-framework-tools')

    implementation project(':jminor-javafx-framework')

    implementation project(':jminor-framework-plugins-jasperreports')
    implementation project(':jminor-framework-plugins-json')

    testImplementation project(':jminor-swing-framework-ui-test')

    implementation 'javax.ws.rs:jsr311-api:1.1.1'
    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtime project(':jminor-framework-plugins-logback-proxy')

    compile("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtime "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtime "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtime "commons-collections:commons-collections:${commonsCollectionsVersion}"
    runtime "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtime "org.eclipse.jdt.core.compiler:ecj:${jdtCoreCompilerVersion}"

    runtime "com.h2database:h2:${h2Version}"
}

test {
    systemProperty "jminor.db.initScript", "src/main/sql/create_schema.sql"
    systemProperty "jminor.client.domainClass", "org.jminor.framework.demos.empdept.domain.EmpDept"
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    doLast {
        def jasperSourceDir = file('src/main/reports')
        def jasperTargetDir = file('build/classes/reports')
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtime.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.dependsOn buildReports

task runClientLocal(type: ModularJavaExec) {
    group 'run'
    main = 'org.jminor.framework.demos.empdept/org.jminor.framework.demos.empdept.client.ui.EmpDeptAppPanel'
    systemProperties = [
            'jminor.client.connectionType': 'local',
            'jminor.db.type'              : 'h2',
            'jminor.db.host'              : 'h2db',
            'jminor.db.embedded'          : 'true',
            'jminor.db.embeddedInMemory'  : 'true',
            'jminor.db.initScript'        : 'src/main/sql/create_schema.sql',
            'jminor.report.path'          : 'build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRemote(type: ModularJavaExec) {
    group 'run'
    main = 'org.jminor.framework.demos.empdept/org.jminor.framework.demos.empdept.client.ui.EmpDeptAppPanel'
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'run'
    main = 'org.jminor.framework.demos.empdept/org.jminor.framework.demos.empdept.client.ui.EmpDeptAppPanel'
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runLoadTestRemote(type: ModularJavaExec) {
    group 'run'
    main = 'org.jminor.framework.demos.empdept/org.jminor.framework.demos.empdept.testing.EmpDeptLoadTest'
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'run'
    main = 'org.jminor.framework.demos.empdept/org.jminor.framework.demos.empdept.testing.EmpDeptLoadTest'
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
}

task clientRemoteScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRemote
    applicationName = project.name + '-remote'
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
}

task loadTestRemoteScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRemote
    applicationName = project.name + '-loadtest-remote'
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/resources/logback.xml'))
    from(files('../../framework/server/src/main/security/jminor_truststore.jks'))
}

applicationDistribution.into('reports') {
    from(fileTree(dir: 'build/classes/reports', include: '*.jasper'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRemoteScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRemoteScript)
}