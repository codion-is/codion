import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply from: '../demos.gradle'

dependencies {
    implementation project(':codion-framework-db-local')
    implementation project(':codion-framework-db-rmi')
    implementation project(':codion-framework-db-http')
    implementation project(':codion-framework-domain-test')
    implementation project(':codion-framework-server')
    implementation project(':codion-framework-servlet')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-tools-ui')
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-swing-framework-tools')

    implementation project(':codion-javafx-framework')

    implementation project(':codion-plugin-jasperreports')
    implementation project(':codion-plugin-json')

    testImplementation project(':codion-swing-framework-ui-test')

    runtimeOnly project(':codion-plugin-logback-proxy')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt.core.compiler:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    mainClass = "$moduleName/is.codion.framework.demos.empdept.ui.EmpDeptAppPanel"
    fxMainClass = "$moduleName/is.codion.framework.demos.empdept.javafx.EmpDeptFX"
    loadTestMainClass = "$moduleName/is.codion.framework.demos.empdept.testing.EmpDeptLoadTest"
    testAddModules = ['is.codion.swing.framework.ui.test']
    testAddReads = ['is.codion.framework.demos.empdept': 'is.codion.swing.framework.ui.test']
    testAddExports = [
            'is.codion.framework.demos.empdept/is.codion.framework.demos.empdept.ui': 'is.codion.swing.framework.ui,is.codion.swing.framework.ui.test',
            'org.junit.platform.commons/org.junit.platform.commons.util': 'ALL-UNNAMED',
            'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
    ]
}

javafx {
    version = '11'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.swing']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
        addExports = testAddExports
    }
    systemProperty "codion.db.initScript", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.empdept.domain.EmpDept"
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/empdept/domain')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.finalizedBy buildReports

task runClientLocal(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScript'        : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientLocalFX(type: ModularJavaExec) {
    group 'run'
    main = 'is.codion.framework.demos.empdept.javafx.EmpDeptFX'
    main = fxMainClass
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScript'        : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/codion_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : 'localhost',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runLoadTestRMI(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/codion_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : 'localhost',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=local',
            '-Dcodion.db.url=jdbc:h2:mem:h2db',
            '-Dcodion.db.initScript=../config/create_schema.sql',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/codion_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/codion_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/resources/logback.xml'))
    from(files('../../framework/server/src/main/security/codion_truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRMIScript)
}