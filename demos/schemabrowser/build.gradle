import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply from: '../demos.gradle'

dependencies {
    implementation project(':jminor-swing-common-tools')
    implementation project(':jminor-swing-common-tools-ui')
    implementation project(':jminor-swing-framework-ui')
    implementation project(':jminor-swing-framework-tools')

    runtimeOnly project(':jminor-plugin-logback-proxy')
    
    runtimeOnly project(':jminor-framework-db-local')
    runtimeOnly project(':jminor-framework-db-remote')
    runtimeOnly project(':jminor-framework-db-http')

    testImplementation project(':jminor-framework-domain-test')
    testImplementation project(':jminor-swing-framework-ui-test')

    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtimeOnly project(':jminor-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    mainClass = "$moduleName/org.jminor.framework.demos.schemabrowser.client.ui.SchemaBrowserAppPanel"
    loadTestMainClass = "$moduleName/org.jminor.framework.demos.schemabrowser.testing.SchemaBrowserLoadTest"
    testAddModules = ['org.jminor.framework.db.test']
    testAddReads = ['org.jminor.framework.demos.schemabrowser': 'org.jminor.framework.db.test']
    testAddExports = [
            'org.jminor.framework.demos.schemabrowser/org.jminor.framework.demos.schemabrowser.domain': 'org.jminor.framework.db.local',
            'org.junit.platform.commons/org.junit.platform.commons.util': 'ALL-UNNAMED',
            'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
    ]
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
        addExports = testAddExports
    }
    systemProperty "jminor.db.initScript", "src/main/sql/create_schema.sql"
    systemProperty "jminor.client.domainClass", "org.jminor.framework.demos.schemabrowser.domain.SchemaBrowser"
}

task runClientLocal(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType': 'local',
            'jminor.db.type'              : 'h2',
            'jminor.db.host'              : 'h2db',
            'jminor.db.embeddedInMemory'  : 'true',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runLoadTestRMI(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=local',
            '-Djminor.db.type=h2',
            '-Djminor.db.host=h2db',
            '-Djminor.db.embedded=true',
            '-Djminor.db.embeddedInMemory=true',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRemoteScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=remote',
            '-Djminor.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=http',
            '-Djminor.client.http.secure=false',
            '-Djminor.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestRemoteScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=remote',
            '-Djminor.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=http',
            '-Djminor.client.http.secure=false',
            '-Djminor.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/resources/logback.xml'))
    from(files('../../framework/server/src/main/security/jminor_truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRemoteScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRemoteScript)
}