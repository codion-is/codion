import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

apply from: '../demos.gradle'

dependencies {
    implementation project(':codion-common-rmi')

    implementation project(':codion-framework-db-local')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-tools-ui')
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-swing-framework-tools')

    implementation project(':codion-javafx-framework')

    implementation project(':codion-plugin-imagepanel')
    implementation project(':codion-plugin-jasperreports')
    implementation project(':codion-plugin-ikonli-foundation')

    runtimeOnly project(':codion-plugin-logback-proxy')

    implementation project(':codion-framework-db-http')
    implementation project(':codion-framework-db-rmi')

    implementation 'com.formdev:flatlaf:1.6.4'

    testImplementation project(':codion-framework-domain-test')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    execMainClass = "is.codion.framework.demos.chinook.ui.ChinookAppPanel"
    execLoadTestMainClass = "is.codion.framework.demos.chinook.testing.ChinookLoadTest"
    testAddModules = ['is.codion.framework.domain.test']
    testAddReads = [
            'is.codion.framework.demos.chinook': 'is.codion.framework.domain.test',
            'is.codion.framework.domain': 'is.codion.framework.demos.chinook'
    ]
}

javafx {
    version = '12'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.swing']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
    systemProperty "codion.db.initScripts", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.chinook.domain.impl.ChinookImpl"
}

application {
    mainClass = execMainClass
    mainModule = moduleName
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/chinook/domain')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.finalizedBy buildReports

compileTestJava.dependsOn buildReports

task runClientLocal(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts'       : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}",
            'logback.configurationFile'   : 'src/main/config/logback.xml'
    ]
}

task runLoadTestRMI(type: ModularJavaExec) {
    group 'application'
    mainClass = execLoadTestMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'application'
    mainClass = execLoadTestMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}",
            'logback.configurationFile'   : 'src/main/config/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=local',
            '-Dcodion.db.url=jdbc:h2:mem:h2db',
            '-Dcodion.db.initScripts=../config/create_schema.sql',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
    from(files('../../framework/server/src/main/security/truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRMIScript)
}