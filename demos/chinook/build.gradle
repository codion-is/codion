apply from: '../demos.gradle'

dependencies {
    implementation project(':jminor-common-core')
    implementation project(':jminor-common-db')
    implementation project(':jminor-common-model')

    implementation project(':jminor-framework-domain')
    implementation project(':jminor-framework-db-core')
    implementation project(':jminor-framework-db-local')
    implementation project(':jminor-framework-model')

    implementation project(':jminor-swing-common-model')
    implementation project(':jminor-swing-common-ui')
    implementation project(':jminor-swing-common-tools')
    implementation project(':jminor-swing-common-tools-ui')
    implementation project(':jminor-swing-framework-model')
    implementation project(':jminor-swing-framework-ui')
    implementation project(':jminor-swing-framework-tools')

    implementation project(':jminor-javafx-framework')

    implementation project(':jminor-plugin-jasperreports')

    runtimeOnly project(':jminor-plugin-logback-proxy')

    runtimeOnly project(':jminor-framework-db-http')
    runtimeOnly project(':jminor-framework-db-remote')

    testImplementation project(':jminor-framework-db-test')

    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    compile("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtime "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtime "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtime "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtime "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtime "org.eclipse.jdt.core.compiler:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':jminor-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

test {
    systemProperty "jminor.db.initScript", "src/main/sql/create_schema.sql"
    systemProperty "jminor.client.domainClass", "org.jminor.framework.demos.chinook.domain.impl.ChinookImpl"
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file('build/classes/reports')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtime.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.dependsOn buildReports

startScripts {
    mainClassName = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
}

clientRemoteScript {
    mainClassName = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
}

clientHttpScript {
    mainClassName = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
}

loadTestRemoteScript {
    mainClassName = 'org.jminor.framework.demos.chinook.testing.ChinookLoadTest'
}

loadTestHttpScript {
    mainClassName = 'org.jminor.framework.demos.chinook.testing.ChinookLoadTest'
}

runClientLocal {
    main = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
}

runClientRemote {
    main = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'jminor.report.path'              : '../../demos/chinook/build/classes/reports',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/resources/logback.xml'
    ]
}

runClientHttp {
    main = 'org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel'
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

runLoadTestRemote {
    main = 'org.jminor.framework.demos.chinook.testing.ChinookLoadTest'
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'jminor.report.path'              : '../../demos/chinook/build/classes/reports',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/resources/logback.xml'
    ]
}

runLoadTestHttp {
    main = 'org.jminor.framework.demos.chinook.testing.ChinookLoadTest'
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}