import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.14'
}

apply from: '../demos.gradle'

apply plugin: BuildReportsPlugin

dependencies {
    implementation project(':codion-common-rmi')

    implementation project(':codion-framework-db-local')

    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-tools-ui')
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-swing-framework-tools')

    implementation project(':codion-javafx-framework')

    implementation project(':codion-plugin-imagepanel')
    implementation project(':codion-plugin-jasperreports')

    runtimeOnly project(':codion-plugin-logback-proxy')

    implementation project(':codion-framework-db-http')
    implementation project(':codion-framework-db-rmi')

    implementation(libs.flatlaf)
    implementation(libs.flatlaf.intellij.themes)

    testImplementation project(':codion-framework-domain-test')

    implementation(libs.jasperreports) {
        transitive = false
    }

    runtimeOnly(libs.commons.logging)
    runtimeOnly(libs.commons.digester)
    runtimeOnly(libs.commons.collections4)
    runtimeOnly(libs.commons.beanutils)
    runtimeOnly(libs.jdt.core.compiler)

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly(libs.h2)
}

ext {
    execMainClass = "is.codion.framework.demos.chinook.ui.ChinookAppPanel"
    execLoadTestMainClass = "is.codion.framework.demos.chinook.testing.ChinookLoadTest"
    testAddModules = ['is.codion.framework.domain.test']
    testAddReads = [
            'is.codion.framework.demos.chinook': 'is.codion.framework.domain.test',
            'is.codion.framework.domain': 'is.codion.framework.demos.chinook'
    ]
}

javafx {
    version = '18'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.swing']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

application {
    mainClass.set(execMainClass)
    mainModule.set(moduleName)
}

buildReports {
    sourceDir = file('src/main/reports')
    targetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/chinook/domain')
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
    systemProperty "codion.db.initScripts", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.chinook.domain.impl.ChinookImpl"
}

tasks.register('runClientLocal', ModularJavaExec) {
    group 'application'
    mainClass.set(execMainClass)
    mainModule.set(moduleName)
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts'       : 'src/main/sql/create_schema.sql'
    ]
}

tasks.register('runClientRMI', ModularJavaExec) {
    group 'application'
    mainClass.set(execMainClass)
    mainModule.set(moduleName)
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'codion.client.trustStore'        : '../../framework/server/src/main/config/truststore.jks',
            'codion.client.trustStorePassword': 'crappypass'
    ]
}

tasks.register('runClientHttp', ModularJavaExec) {
    group 'application'
    mainClass.set(execMainClass)
    mainModule.set(moduleName)
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}"
    ]
}

tasks.register('runLoadTestRMI', ModularJavaExec) {
    group 'application'
    mainClass.set(execLoadTestMainClass)
    mainModule.set(moduleName)
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'codion.client.trustStore'        : '../../framework/server/src/main/config/truststore.jks',
            'codion.client.trustStorePassword': 'crappypass'
    ]
}

tasks.register('runLoadTestHttp', ModularJavaExec) {
    group 'application'
    mainClass.set(execLoadTestMainClass)
    mainModule.set(moduleName)
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook']
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}"
    ]
}

tasks.register('clientLocalScript', ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=local',
            '-Dcodion.db.url=jdbc:h2:mem:h2db',
            '-Dcodion.db.initScripts=../config/create_schema.sql'
    ]
}

tasks.register('clientRMIScript', ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=remote',
            "-Dcodion.server.hostname=" + "${serverHostName}",
            '-Dcodion.client.trustStore=../config/truststore.jks',
            '-Dcodion.client.trustStorePassword=crappypass'
    ]
}

tasks.register('clientHttpScript', ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            "-Dcodion.client.http.hostname=" + "${serverHostName}",
    ]
}

tasks.register('loadTestRMIScript', ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=remote',
            "-Dcodion.server.hostname=" + "${serverHostName}",
            '-Dcodion.client.trustStore=../config/truststore.jks',
            '-Dcodion.client.trustStorePassword=crappypass'
    ]
}

tasks.register('loadTestHttpScript', ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.chinook',
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            "-Dcodion.client.http.hostname=" + "${serverHostName}",
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('../../framework/server/src/main/config/truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRMIScript)
}