import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply from: '../demos.gradle'

dependencies {
    implementation project(':jminor-common-remote')

    implementation project(':jminor-framework-db-local')

    implementation project(':jminor-swing-common-tools')
    implementation project(':jminor-swing-common-tools-ui')
    implementation project(':jminor-swing-framework-ui')
    implementation project(':jminor-swing-framework-tools')

    implementation project(':jminor-javafx-framework')

    implementation project(':jminor-plugin-jasperreports')

    runtimeOnly project(':jminor-plugin-logback-proxy')

    runtimeOnly project(':jminor-framework-db-http')
    runtimeOnly project(':jminor-framework-db-remote')

    testImplementation project(':jminor-framework-domain-test')

    implementation "org.jfree:jcommon:${jcommonVersion}"
    implementation "org.jfree:jfreechart:${jfreeChartVersion}"

    runtimeOnly project(':jminor-plugin-logback-proxy')

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt.core.compiler:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':jminor-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    mainClass = "$moduleName/org.jminor.framework.demos.chinook.client.ui.ChinookAppPanel"
    loadTestMainClass = "$moduleName/org.jminor.framework.demos.chinook.testing.ChinookLoadTest"
    testAddModules = ['org.jminor.framework.db.test']
    testAddReads = ['org.jminor.framework.demos.chinook': 'org.jminor.framework.db.test']
}

javafx {
    version = '11'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.swing']
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
    systemProperty "jminor.db.initScript", "src/main/sql/create_schema.sql"
    systemProperty "jminor.client.domainClass", "org.jminor.framework.demos.chinook.domain.impl.ChinookImpl"
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file('build/classes/reports')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.dependsOn buildReports

task runClientLocal(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType': 'local',
            'jminor.db.type'              : 'h2',
            'jminor.db.host'              : 'h2db',
            'jminor.db.embedded'          : 'true',
            'jminor.db.embeddedInMemory'  : 'true',
            'jminor.db.initScript'        : 'src/main/sql/create_schema.sql',
            'jminor.report.path'          : 'build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'jminor.report.path'              : '../../demos/chinook/build/classes/reports',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'run'
    main = mainClass
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runLoadTestRMI(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'jminor.client.connectionType'    : 'remote',
            'jminor.server.hostname'          : 'localhost',
            'jminor.report.path'              : '../../demos/chinook/build/classes/reports',
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/jminor_truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'run'
    main = loadTestMainClass
    systemProperties = [
            'jminor.client.connectionType': 'http',
            'jminor.client.http.secure'   : 'false',
            'jminor.client.http.hostname' : 'localhost',
            'jminor.report.path'          : '../../demos/chinook/build/classes/reports',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=local',
            '-Djminor.db.type=h2',
            '-Djminor.db.host=h2db',
            '-Djminor.db.embedded=true',
            '-Djminor.db.embeddedInMemory=true',
            '-Djminor.db.initScript=../config/create_schema.sql',
            '-Djminor.report.path=../reports',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=remote',
            '-Djminor.server.hostname=localhost',
            '-Djminor.report.path=../reports',
            '-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=http',
            '-Djminor.client.http.secure=false',
            '-Djminor.client.http.hostname=localhost',
            '-Djminor.report.path=../reports',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=remote',
            '-Djminor.server.hostname=localhost',
            '-Djminor.report.path=../reports',
            '-Djavax.net.ssl.trustStore=../config/jminor_truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '-Djminor.client.connectionType=http',
            '-Djminor.client.http.secure=false',
            '-Djminor.client.http.hostname=localhost',
            '-Djminor.report.path=../reports',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/resources/logback.xml'))
    from(files('../../framework/server/src/main/security/jminor_truststore.jks'))
}

applicationDistribution.into('reports') {
    from(fileTree(dir: 'build/classes/reports', include: '*.jasper'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRMIScript)
}