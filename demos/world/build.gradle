import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply from: '../demos.gradle'

dependencies {
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-plugin-jackson-json')
    implementation project(':codion-plugin-jasperreports')

    implementation "org.jfree:jfreechart:${jfreeChartVersion}"
    implementation 'org.jxmapviewer:jxmapviewer2:2.6'
    implementation "org.json:json:${jsonVersion}"

    testImplementation project(':codion-framework-domain-test')
    testImplementation project(':codion-swing-framework-ui-test')

    runtimeOnly project(':codion-plugin-logback-proxy')

    runtimeOnly project(':codion-framework-db-local')
    runtimeOnly project(':codion-framework-db-rmi')
    runtimeOnly project(':codion-framework-db-http')

    implementation "com.formdev:flatlaf:${flatLafVersion}"
    implementation "com.formdev:flatlaf-intellij-themes:${flatLafVersion}"

    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
        transitive = false
    }

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt:ecj:${jdtCoreCompilerVersion}"

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    execMainClass = "is.codion.framework.demos.world.ui.WorldAppPanel"
    testAddModules = ['is.codion.framework.domain.test', 'is.codion.swing.framework.ui.test']
    testAddReads = [
            'is.codion.framework.demos.world': 'is.codion.framework.domain.test,is.codion.swing.framework.ui.test',
            'is.codion.framework.domain': 'is.codion.framework.demos.world'
    ]
    testAddExports = [
            'is.codion.framework.demos.world/is.codion.framework.demos.world.ui': 'is.codion.swing.framework.ui.test',
            'is.codion.framework.demos.world/is.codion.framework.demos.world.domain': 'is.codion.framework.db.local',
            'org.junit.platform.commons/org.junit.platform.commons.util': 'ALL-UNNAMED',
            'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
    ]
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

application {
    mainClass = execMainClass
    mainModule = moduleName
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    // otherwise this task fails in a clean project clone, since the codion jar files
    // which are required on the report compile classpath have not been created yet
    dependsOn configurations.runtimeClasspath
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/world/model')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            lifecycleLogLevel = "INFO"
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs.asPath)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.finalizedBy buildReports

compileTestJava.dependsOn buildReports

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
        addExports = testAddExports;
    }
    dependsOn buildReports
    systemProperty "codion.db.initScripts", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.world.domain.WorldImpl"
}

domainJar {
    dependsOn buildReports
}

jar {
    dependsOn buildReports
}

task runClientLocal(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world']
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts'       : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/config/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world']
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'codion.client.trustStore'        : '../../framework/server/src/main/config/truststore.jks',
            'codion.client.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    mainModule = moduleName
    jvmArgs = ['--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world']
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}",
            'logback.configurationFile'   : 'src/main/config/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world',
            '-Dcodion.client.connectionType=local',
            '-Dcodion.db.url=jdbc:h2:mem:h2db',
            '-Dcodion.db.initScripts=../config/create_schema.sql',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world',
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Dcodion.client.trustStore=../config/truststore.jks',
            '-Dcodion.client.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '--add-reads', 'is.codion.framework.domain=is.codion.framework.demos.world',
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/config/logback.xml'))
    from(files('../../framework/server/src/main/config/truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
}