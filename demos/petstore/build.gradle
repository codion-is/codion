import org.javamodularity.moduleplugin.tasks.ModularCreateStartScripts
import org.javamodularity.moduleplugin.tasks.ModularJavaExec

apply from: '../demos.gradle'

dependencies {
    implementation project(':codion-swing-common-tools')
    implementation project(':codion-swing-common-tools-ui')
    implementation project(':codion-swing-framework-ui')
    implementation project(':codion-swing-framework-tools')

    runtimeOnly project(':codion-plugin-logback-proxy')

    runtimeOnly project(':codion-framework-db-local')
    runtimeOnly project(':codion-framework-db-rmi')
    runtimeOnly project(':codion-framework-db-http')

    testImplementation project(':codion-framework-domain-test')
    testImplementation project(':codion-swing-framework-ui-test')

    runtimeOnly project(':codion-dbms-h2database')
    runtimeOnly "com.h2database:h2:${h2Version}"
}

ext {
    execMainClass = "$moduleName/is.codion.framework.demos.petstore.ui.PetstoreAppPanel"
    execLoadTestMainClass = "$moduleName/is.codion.framework.demos.petstore.testing.PetstoreLoadTest"
    testAddModules = ['is.codion.framework.domain.test']
    testAddReads = ['is.codion.framework.demos.petstore': 'is.codion.framework.domain.test']
    testAddExports = [
            'is.codion.framework.demos.petstore/is.codion.framework.demos.petstore.domain': 'is.codion.framework.db.local',
            'org.junit.platform.commons/org.junit.platform.commons.util': 'ALL-UNNAMED',
            'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
    ]
}

compileTestJava {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
    }
}

test {
    moduleOptions {
        addModules = testAddModules
        addReads = testAddReads
        addExports = testAddExports
    }
    systemProperty "codion.db.initScripts", "src/main/sql/create_schema.sql"
    systemProperty "codion.client.domainClass", "is.codion.framework.demos.petstore.domain.Petstore"
}

application {
    mainClass = execMainClass
}

task runClientLocal(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    systemProperties = [
            'codion.client.connectionType': 'local',
            'codion.db.url'               : 'jdbc:h2:mem:h2db',
            'codion.db.initScripts'       : 'src/main/sql/create_schema.sql',
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runClientRMI(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runClientHttp(type: ModularJavaExec) {
    group 'application'
    mainClass = execMainClass
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}",
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task runLoadTestRMI(type: ModularJavaExec) {
    group 'application'
    mainClass = execLoadTestMainClass
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHostName}",
            'javax.net.ssl.trustStore'        : '../../framework/server/src/main/security/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'logback.configurationFile'       : 'src/main/config/logback.xml'
    ]
}

task runLoadTestHttp(type: ModularJavaExec) {
    group 'application'
    mainClass = execLoadTestMainClass
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHostName}",
            'logback.configurationFile'   : 'src/main/resources/logback.xml'
    ]
}

task clientLocalScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientLocal
    applicationName = project.name + '-local'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=local',
            '-Dcodion.db.url=jdbc:h2:mem:h2db',
            '-Dcodion.db.initScripts=../config/create_schema.sql',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientRMI
    applicationName = project.name + '-remote'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task clientHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runClientHttp
    applicationName = project.name + '-http'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestRMIScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestRMI
    applicationName = project.name + '-loadtest-remote'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=remote',
            '-Dcodion.server.hostname=localhost',
            '-Djavax.net.ssl.trustStore=../config/truststore.jks',
            '-Djavax.net.ssl.trustStorePassword=crappypass',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

task loadTestHttpScript(type: ModularCreateStartScripts) {
    runTask = tasks.runLoadTestHttp
    applicationName = project.name + '-loadtest-http'
    defaultJvmOpts = [
            '-Dcodion.client.connectionType=http',
            '-Dcodion.client.http.secure=false',
            '-Dcodion.client.http.hostname=localhost',
            '-Dlogback.configurationFile=../config/logback.xml'
    ]
}

applicationDistribution.into('config') {
    from(files('src/main/sql/create_schema.sql'))
    from(files('src/main/resources/logback.xml'))
    from(files('../../framework/server/src/main/security/truststore.jks'))
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(clientLocalScript)
    from(clientRMIScript)
    from(clientHttpScript)
    from(loadTestHttpScript)
    from(loadTestRMIScript)
}